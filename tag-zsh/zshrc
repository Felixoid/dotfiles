zstyle :compinstall filename "${HOME}/.zshrc"
zstyle ':completion:*' completer _complete _ignored _approximate
zstyle ':completion:*' max-errors 1
zstyle ':completion:*' menu select

# I don't want to see error `zsh: no matches found: remote.host:file.*` on `rsync` invoking
unsetopt NOMATCH

# allow comments in interactive
setopt interactivecomments

############################## Prompting ##############################
# Vim modes lag
export KEYTIMEOUT=1

#Prompts
function prompt_char {
    git rev-parse --git-dir >/dev/null 2>&1 && echo '±' && return
    svn info >/dev/null 2>&1 && echo 'ƨ' && return
    hg root >/dev/null 2>&1 && echo '☿' && return
    echo 'ѳ'
}

if [ -r ~/.config/zsh/git-prompt.zsh ]; then
  source ~/.config/zsh/git-prompt.zsh
  _GIT_PROMPT_ENABLED=true
  ZSH_GIT_PROMPT_SHOW_STASH=true
else
  gitprompt() {
    :
  }
fi

function zle-line-init zle-keymap-select {
    # I want to avoid black color, that's why '% 7 + 1'
    local USER_CKSUM="$(cksum <<< $USER)"
    local HOST_CKSUM="$(cksum <<< $HOST)"
    local USER_FG=$(( ${USER_CKSUM/ */} / 13 % 7 + 1))
    local HOST_FG=$(( ${HOST_CKSUM/ */} / 13 % 7 + 1))
    local ROOT_PROMPT="%F{red}%B%n@%m%b%f"
    local USER_PROMPT="%F{$USER_FG}%n%f@%F{$HOST_FG}%m%f"
    [ "${SSH_CLIENT}" ] && local SSH_PROMPT="%F{yellow}[ssh from ${SSH_CLIENT%% *}]%f "
    [ "${VIRTUAL_ENV}" ] && local VENV="%F{green}venv=($(basename $VIRTUAL_ENV))%f "
    case $KEYMAP in
      viins|main) local VI="%F{2}%(!.#.>)$(prompt_char)>%f" ;;
      vicmd)      local VI="%F{3}<$(prompt_char)%(!.#.<)%f" ;;
    esac
    export PS1="┌%(?..%F{1})[%D{%H:%M:%S}]%f %(!.${ROOT_PROMPT}.${USER_PROMPT}):%~ ${VENV}${SSH_PROMPT}$(gitprompt)"$'\n'"└${VI} "
    RPROMPT="%(?..%F{1}[e:%?]%f)"
    zle reset-prompt
    # this line fixes the issue with errasing last line in vim mode by Alt+A or Alt+I
    zle -R
}
zle -N zle-line-init
zle -N zle-keymap-select

#################### Prompting end ####################

# Global cross-shells
[ -f ${HOME}/.config/shells/rc ] && . ${HOME}/.config/shells/rc

# Include bash profile by default
emulate sh -c 'source /etc/profile'

# Unlock ssh rsa keys, defined in ${HOME}/.config/shells/rc
zle -N unlock-rsa

# Switcher for git prompt cache
_git_prompt_cache_switch() {
  if [ -z ${ZSH_THEME_GIT_PROMPT_CACHE} ]; then
    export ZSH_THEME_GIT_PROMPT_CACHE=true
  else
    unset ZSH_THEME_GIT_PROMPT_CACHE
  fi
}
zle -N _git_prompt_cache_switch

autoload -Uz compinit
compinit

bindkey -v
bindkey -M viins '\e.' insert-last-word
bindkey "^W" backward-kill-word
bindkey '^K' unlock-rsa
bindkey '^G' _git_prompt_cache_switch
bindkey "^[[A" history-beginning-search-backward
bindkey "^[OA" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward
bindkey "^[OB" history-beginning-search-forward

# Terminal title
case ${TERM} in
  xterm*)
    precmd () {print -Pn "\e]0;%5~\a"}
    ;;
esac

# autocompletion
_SERVERADMIN_HOSTS_ZSTYLE=true

# work rcs
if [ -r "$HOME/.config/workrc/" ] \
    && [ -n "$(ls "$HOME/.config/workrc/")" ]; then
  for src in "$HOME/.config/workrc/"*.zsh(N); do
    [ -r "$src" ] && . "$src"
  done
fi
